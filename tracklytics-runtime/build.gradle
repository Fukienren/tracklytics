import com.android.build.gradle.LibraryPlugin
import org.aspectj.bridge.IMessage
import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'org.aspectj:aspectjtools:1.8.6'
  }
}

apply plugin: 'com.android.library'
apply from: '../maven_push.gradle'

repositories {
  maven { url 'https://maven.fabric.io/public' }
}

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.1"

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  defaultConfig {
    minSdkVersion 10

    def fabricKey = ''
    if (file('../tracklytics.properties').exists()) {
      def Properties keys = new Properties()
      keys.load(new FileInputStream(file('../tracklytics.properties')))
      fabricKey = keys["fabric"]
    }
    manifestPlaceholders = [FABRIC_API_KEY: fabricKey]
  }

  lintOptions {
    abortOnError false
  }
}

configurations {
  optional
  compile.extendsFrom optional
}

dependencies {
  provided 'org.aspectj:aspectjrt:1.8.6'

  // Fabric
  provided('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
    transitive = true;
  }

  //  Adjust
  provided 'com.adjust.sdk:adjust-android:4.1.3'

  provided 'com.mixpanel.android:mixpanel-android:4.6.4'
  provided 'com.google.android.gms:play-services-base:8.3.0'
  provided 'com.google.android.gms:play-services-gcm:8.3.0'

  // Adjust uses it to get unique identifier
  provided 'com.google.android.gms:play-services-ads:8.3.0'

  //Crittercism
  provided 'com.crittercism:crittercism-android-agent:5.5.0-rc-1'

  //google analytics
  provided 'com.google.android.gms:play-services-analytics:8.3.0'

  testCompile 'junit:junit:4.12'
  testCompile "org.mockito:mockito-core:1.9.5"
  testCompile 'org.assertj:assertj-core:1.7.0'
  testCompile "org.robolectric:robolectric:3.0"
}

android.libraryVariants.all { variant ->
  LibraryPlugin plugin = project.plugins.getPlugin(LibraryPlugin)
  JavaCompile javaCompile = variant.javaCompile
  javaCompile.doLast {
    String[] args = [
        "-showWeaveInfo",
        "-1.5",
        "-inpath", javaCompile.destinationDir.toString(),
        "-aspectpath", javaCompile.classpath.asPath,
        "-d", javaCompile.destinationDir.toString(),
        "-classpath", javaCompile.classpath.asPath,
        "-bootclasspath", plugin.project.android.bootClasspath.join(File.pathSeparator)
    ]

    MessageHandler handler = new MessageHandler(true);
    new Main().run(args, handler)

    def log = project.logger
    for (IMessage message : handler.getMessages(null, true)) {
      switch (message.getKind()) {
        case IMessage.ABORT:
        case IMessage.ERROR:
        case IMessage.FAIL:
          log.error message.message, message.thrown
          break;
        case IMessage.WARNING:
        case IMessage.INFO:
          log.info message.message, message.thrown
          break;
        case IMessage.DEBUG:
          log.debug message.message, message.thrown
          break;
      }
    }
  }
}